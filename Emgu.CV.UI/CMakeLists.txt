# --------------------------------------------------------
#  Copyright (C) 2004-2017 by EMGU Corporation. All rights reserved.
# --------------------------------------------------------

IF((EMGU_CV_BUILD OR EMGU_CV_EXAMPLE_BUILD) AND NOT ANDROID AND NOT NETFX_CORE AND NOT APPLE)

PROJECT(Emgu.CV.UI)

MESSAGE(STATUS "Configuring Emgu.CV.UI")
FILE(GLOB_RECURSE SRC_CV_UI RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} "*.cs")
LIST(APPEND SRC_CV_UI ${CMAKE_CURRENT_SOURCE_DIR}/../CommonAssemblyInfo.cs)

GENERATE_DOCUMENT(${LIBRARY_OUTPUT_PATH}/${PROJECT_NAME})
SIGN_ASSEMBLY(${CMAKE_CURRENT_SOURCE_DIR}/../Emgu.CV.snk)
SET_CS_TARGET_FRAMEWORK(${CSC_TARGET_FRAMEWORK})
ADD_CS_REFERENCES("${LIBRARY_OUTPUT_PATH}/Emgu.CV.World.dll;${LIBRARY_3RDPARTY_PATH}/ZedGraph.dll")
ADD_CS_FRAMEWORK_REFERENCES("${CSC_TARGET_FRAMEWORK}" "System.Windows.Forms.dll")

FILE(GLOB RESX_SRC 
   "${CMAKE_CURRENT_SOURCE_DIR}/*.resx" 
   "${CMAKE_CURRENT_SOURCE_DIR}/Properties/*.resx")
FOREACH (RESX ${RESX_SRC})
   STRING(REPLACE "${CMAKE_CURRENT_SOURCE_DIR}/" "${PROJECT_NAME}." RESOURCES_NAME ${RESX})
   STRING(REPLACE ".resx" ".resources" RESOURCES_NAME ${RESOURCES_NAME})
   STRING(REPLACE "/" "." RESOURCES_NAME ${RESOURCES_NAME})
   ADD_CS_RESOURCES(${RESX} ${RESOURCES_NAME})
ENDFOREACH(RESX)

ADD_CS_FILE_TO_DEPLOY(${CMAKE_CURRENT_SOURCE_DIR}/../lib/3rdParty/ZedGraph.dll)
COMPILE_CS(${PROJECT_NAME} "library" "${SRC_CV_UI}" ALL)
#BUILD_CSPROJ(Emgu.CV.UI "${CMAKE_CURRENT_SOURCE_DIR}/Emgu.CV.UI.csproj")
if(ENABLE_SOLUTION_FOLDERS)
    set_target_properties(${PROJECT_NAME} PROPERTIES FOLDER "emgu")
endif()

ADD_DEPENDENCIES(${PROJECT_NAME} Emgu.CV.World)
INSTALL(
FILES 
${CMAKE_CURRENT_SOURCE_DIR}/../lib/3rdParty/ZedGraph.dll
${CMAKE_CURRENT_SOURCE_DIR}/../lib/3rdParty/ZedGraph.license.txt
${LIBRARY_OUTPUT_PATH}/${PROJECT_NAME}.dll 
${LIBRARY_OUTPUT_PATH}/${PROJECT_NAME}.xml
DESTINATION ${CPACK_PACKAGE_CLI_FOLDER}
COMPONENT emgucv_binary)

IF(WIN32)
INSTALL(
  DIRECTORY
  ${CMAKE_CURRENT_SOURCE_DIR}
  DESTINATION .
    COMPONENT emgucv_source
  FILES_MATCHING 
  PATTERN "*.cs"
  PATTERN "*.csproj"
  PATTERN "*.resx"
  PATTERN ".svn" EXCLUDE
  PATTERN "obj" EXCLUDE
  PATTERN "CMakeFiles" EXCLUDE
  PATTERN "${PROJECT_NAME}.dir" EXCLUDE
)
ENDIF()

ENDIF()
